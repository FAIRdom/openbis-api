apply plugin: 'java'
apply plugin: 'application'
mainClassName = 'org.fairdom.OpenSeekEntry'
apply plugin: 'jacoco' // test reporter
apply plugin: 'com.github.kt3k.coveralls' //coveralls plugin
sourceCompatibility=1.7
targetCompatibility=1.7


repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.fasterxml.jackson.core:jackson-core:+',
            'com.fasterxml.jackson.core:jackson-annotations:+',
            'com.fasterxml.jackson.core:jackson-databind:+',
            'org.reflections:reflections:0.9.9-RC1',
            'com.googlecode.json-simple:json-simple:1.1'
}

task copyJarFiles(type: Copy){
    from configurations.compile
    into "$buildDir/libs/lib"
}

task archiveJar(type: Jar, dependsOn: copyJarFiles) {
    File libDir = file("$buildDir/libs/lib")    
    FileCollection collection = files{ libDir.listFiles() }
    def classPath = []
    collection.each {File file ->
        classPath << 'lib/' + file.name
    }

    def classPathJoined = classPath.join(' ')
    baseName = 'openseek-api'
    version =  '0.5.1'
    manifest.attributes('Main-Class': 'org.fairdom.OpenSeekEntry',
                        'Class-Path': "$classPathJoined")
    with jar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}


run {
    if ( project.hasProperty("appArgs") ) {
        args Eval.me(appArgs)
    }
}


// This part is for sending test report to coveralls
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0x'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
